plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

group = "com.dosei.music"
version = "1.3.1"

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.3"
    }
    namespace 'com.dosei.music.arpeggio'
}

dependencies {
    //region Android/Kotlin

    implementation libs.core.ktx
    implementation libs.app.compat
    implementation libs.android.material
    implementation libs.lifecycle.runtime

    //endregion

    //region Unit Tests

    testImplementation libs.junit.core

    //endregion

    //region UI

    implementation platform(libs.compose.bom)
    implementation libs.compose.ui
    implementation libs.compose.material
    implementation libs.compose.graphics
    implementation libs.compose.preview
    implementation libs.activity.compose

    debugImplementation libs.compose.tooling.ui
    debugImplementation libs.compose.test.manifest

    androidTestImplementation(platform(libs.compose.bom))
    androidTestImplementation libs.android.test.ext
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.compose.junit

    //endregion
}

afterEvaluate {
    publishing {
        def secrets = new Properties()
        secrets.load(new FileInputStream(file("${rootDir}/secret.properties")))

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/JoaoGeniselli/Arpeggio")
                credentials {
                    username = secrets["publishUser"] as String
                    password = secrets["publishToken"] as String
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.release)
            }
        }
    }
}